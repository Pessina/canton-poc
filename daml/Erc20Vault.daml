module Erc20Vault where

import DA.Crypto.Text (BytesHex, PublicKeyHex, SignatureHex, secp256k1WithEcdsaOnly)

import Types (EvmTransactionParams)
import Crypto (computeRequestId, computeResponseHash, chainIdToDecimalText)

template Erc20Holding
  with
    issuer       : Party
    owner        : Party
    erc20Address : BytesHex
    amount       : Decimal
  where
    signatory issuer
    observer owner

template PendingEvmDeposit
  with
    issuer    : Party
    requester : Party
    requestId : BytesHex
    path      : Text
    evmParams : EvmTransactionParams
  where
    signatory issuer
    observer requester

template EcdsaSignature
  with
    issuer    : Party
    requestId : BytesHex
    r         : BytesHex
    s         : BytesHex
    v         : Int
  where
    signatory issuer

template EvmTxOutcomeSignature
  with
    issuer    : Party
    requestId : BytesHex
    signature : SignatureHex
    mpcOutput : BytesHex
  where
    signatory issuer

template VaultOrchestrator
  with
    issuer       : Party
    mpcPublicKey : PublicKeyHex
  where
    signatory issuer

    nonconsuming choice RequestEvmDeposit : ContractId PendingEvmDeposit
      with
        requester : Party
        path      : Text
        evmParams : EvmTransactionParams
      controller issuer, requester
      do
        let sender = partyToText requester
        let caip2Id = "eip155:" <> chainIdToDecimalText evmParams.chainId
        let requestId = computeRequestId sender evmParams caip2Id 1 path
        create PendingEvmDeposit with
          issuer; requester; requestId; path; evmParams

    nonconsuming choice SignEvmTx : ContractId EcdsaSignature
      with
        requestId : BytesHex
        r         : BytesHex
        s         : BytesHex
        v         : Int
      controller issuer
      do
        create EcdsaSignature with
          issuer; requestId; r; s; v

    nonconsuming choice ProvideEvmOutcomeSig : ContractId EvmTxOutcomeSignature
      with
        requestId : BytesHex
        signature : SignatureHex
        mpcOutput : BytesHex
      controller issuer
      do
        create EvmTxOutcomeSignature with
          issuer; requestId; signature; mpcOutput

    nonconsuming choice ClaimEvmDeposit : ContractId Erc20Holding
      with
        pendingCid  : ContractId PendingEvmDeposit
        outcomeCid  : ContractId EvmTxOutcomeSignature
        amount      : Decimal
      controller issuer
      do
        pending <- fetch pendingCid
        outcome <- fetch outcomeCid

        assertMsg "Request ID mismatch"
          (pending.requestId == outcome.requestId)

        assertMsg "MPC reported ETH transaction failure"
          (outcome.mpcOutput == "01")

        let responseHash = computeResponseHash pending.requestId outcome.mpcOutput
        assertMsg "Invalid MPC signature on deposit response"
          (secp256k1WithEcdsaOnly outcome.signature responseHash mpcPublicKey)

        archive pendingCid
        archive outcomeCid

        create Erc20Holding with
          issuer
          owner = pending.requester
          erc20Address = (pending.evmParams).to
          amount
